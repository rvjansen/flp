package com.rvjansen
import javax.swing.

class PapWordsListModel extends AbstractListModel implements ComboBoxModel

  properties public
  PapWords = ArrayList()
  selection = String

  method PapWordsListModel
    do
      client = KBClient('')
      i = client.request('pap(X),nm(X,Y).').iterator()
      loop while i.hasNext()
	w = Rexx i.next()
	PapWords.add(w['Y'].strip('b',"'") w['X'])
      end
    catch java.rmi.RemoteException
      exit
    end
    Collections.sort(PapWords)
    
  method getElementAt(index=int) returns Object
    return PapWords.get(index) 

  method getSize() returns int
    return PapWords.size()

  method setSelectedItem(anItem=Object)
    selection = Rexx anItem  --to select and register an item from the pull down list

  -- Methods implemented from the interface ComboBoxModel
  method getSelectedItem() returns Object
    return selection -- to add the selection to the combo box

  method lookup(s=String) returns int
    sr = Rexx s
    count  = 0
    i = PapWords.iterator()
    loop while i.hasNext()
      r = Rexx i.next()
      if r.left(s.length()) = s then do
	return count
      end
      count = count + 1
    end -- loop while i
    return 0 